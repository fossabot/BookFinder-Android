apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "$rootDir/common-methods.gradle"
apply from: 'build-config.gradle'

final userName = System.getProperty("user.name")
final credentialsFolder = getCredentialsFolder(userName)

final releaseKeystoreProperties = getKeystoreProperties(credentialsFolder + "release-keystore.properties")
final debugKeystoreProperties = getKeystoreProperties(credentialsFolder + "debug-keystore.properties")

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId appId
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: appId]
                includeCompileClasspath = true
            }
        }
    }
    signingConfigs {
        release {
            storeFile file(credentialsFolder + releaseKeystoreProperties['file'])
            keyAlias releaseKeystoreProperties['alias']
            keyPassword releaseKeystoreProperties['password']
            storePassword releaseKeystoreProperties['keystorePassword']
        }
        debug {
            storeFile file(credentialsFolder + debugKeystoreProperties['file'])
            keyAlias debugKeystoreProperties['alias']
            keyPassword debugKeystoreProperties['password']
            storePassword debugKeystoreProperties['keystorePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true
            minifyEnabled true
            debuggable false
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

ext {
    support_library_version = "27.1.1"
    moxy_version = "1.5.3"
    toothpick_version = "1.1.2"
    room_version = "1.0.0"
    picasso_version = "2.71828"
    retrofit_version = "2.4.0"
    junit_version = "4.12"
    mockito_version = "2.8.9"
    mockito_kt_version = "1.5.0"
}

dependencies {
    implementation(
            "org.jetbrains.anko:anko:$anko_version",
            "org.jetbrains.anko:anko-recyclerview-v7:$anko_version",
            "com.android.support:appcompat-v7:$support_library_version",
            "com.android.support:recyclerview-v7:$support_library_version",
            "com.android.support:cardview-v7:$support_library_version",
            "com.android.support:transition:$support_library_version",
            "com.arello-mobile:moxy-compiler:$moxy_version",
            "com.arello-mobile:moxy-app-compat:$moxy_version",
            "com.github.stephanenicolas.toothpick:toothpick-runtime:$toothpick_version",
            "com.squareup.picasso:picasso:$picasso_version",
            "com.squareup.retrofit2:retrofit:$retrofit_version",
            "android.arch.persistence.room:runtime:$room_version",
    )
    testImplementation(
            "junit:junit:$junit_version",
            "org.mockito:mockito-core:$mockito_version",
            "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kt_version",
    )
    kapt(
            "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothpick_version",
            "com.arello-mobile:moxy-compiler:$moxy_version",
            "android.arch.persistence.room:compiler:$room_version",
    )
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}